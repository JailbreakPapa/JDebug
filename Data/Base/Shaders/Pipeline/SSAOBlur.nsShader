[PLATFORMS]
ALL

[PERMUTATIONS]

CAMERA_MODE
VERTEX_SHADER_RENDER_TARGET_ARRAY_INDEX

[RENDERSTATE]

DepthTest = false
DepthWrite = false
CullMode = CullMode_None

[VERTEXSHADER]

#include <Shaders/Pipeline/FullscreenTriangleVertexShader.h>

[GEOMETRYSHADER]

#include <Shaders/Pipeline/FullscreenTriangleStereoGeometryShader.h>

[PIXELSHADER]

#include <Shaders/Pipeline/FullscreenTriangleInterpolator.h>
#include <Shaders/Common/Common.h>
#include <Shaders/Common/GlobalConstants.h>
#include "SSAOConstants.h"

Texture2DArray SSAOTexture;

float CalculateDepthWeight(float depthA, float depthB, float scale)
{
  return saturate(1 - abs(depthA - depthB) * scale);
}

float4 main(PS_IN Input) : SV_Target
{
#if CAMERA_MODE == CAMERA_MODE_STEREO
  s_ActiveCameraEyeIndex = Input.RenderTargetArrayIndex;
#endif

  float2 ssaoAndCenterDepth = SSAOTexture.Sample(PointClampSampler, float3(Input.TexCoord0, s_ActiveCameraEyeIndex)).rg;

  float centerDepth = ssaoAndCenterDepth.y;
  if (centerDepth >= ClipPlanes.y)
  {
    return float4(1, centerDepth, 0, 1);
  }

  float totalSSAO = ssaoAndCenterDepth.x;
  float totalWeight = 1.0f;

  float2 offsets[16] =
  {
    float2(1, 1),
    float2(0, 1),
    float2(-1, 1),
    float2(-1, 0),
    float2(-1, -1),
    float2(0, -1),
    float2(1, -1),
    float2(1, 0),
    float2(1, 2),
    float2(-1, 2),
    float2(1, -2),
    float2(-1, -2),
    float2(2, 1),
    float2(2, -1),
    float2(-2, 1),
    float2(-2, -1),
  };

  float2 blurRadius = ViewportSize.zw;

  [unroll]
  for (int i = 0; i < 16; ++i)
  {
    float2 ssaoAndDepth = SSAOTexture.SampleLevel(PointClampSampler, float3(Input.TexCoord0 + offsets[i] * blurRadius, s_ActiveCameraEyeIndex), 0.0f).rg;
    float weight = CalculateDepthWeight(centerDepth, ssaoAndDepth.y, DepthBlurScale) / length(offsets[i]);

    totalSSAO += ssaoAndDepth.x * weight;
    totalWeight += weight;
  }

  return float4(totalSSAO / totalWeight, centerDepth, 0, 1);
}

